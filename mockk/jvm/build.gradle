plugins {
    id("mpp-jvm")
}

ext {
    mavenName = "MockK"
    mavenDescription = "mocking library for Kotlin"
}

apply from: "${gradles}/jacoco.gradle"
apply from: "${gradles}/additional-archives.gradle"
apply from: "${gradles}/upload.gradle"

dependencies {
    expectedBy project(':mockk-common')
    api project(':mockk-dsl-jvm')
    implementation project(":mockk-agent-jvm")

    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compileOnly "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    compileOnly 'org.slf4j:slf4j-api:1.7.26'

    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
}

evaluationDependsOn(":mockk-common")
task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from(sourceSets.main.allSource)
    from(project(':mockk-common').sourcesJar.inputs.files) {
        exclude "io/mockk/impl/InternalPlatform.kt"
        exclude "io/mockk/impl/annotations/AdditionalInterface.kt"
        exclude "io/mockk/MockK.kt"
    }
}

compileTestKotlin.kotlinOptions.javaParameters = true

jar.archiveBaseName.set('mockk')
sourcesJar.archiveBaseName.set('mockk')
